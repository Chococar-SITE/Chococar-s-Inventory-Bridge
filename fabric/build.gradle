plugins {
    id 'fabric-loom' version '1.11.4'
    id 'maven-publish'
}

version = project.version
group = project.group

base {
    archivesName = "chococars-inventory-bridge-fabric"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "chococars_inventory_bridge" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
            sourceSet sourceSets.test
        }
    }
}

dependencies {
    // Fabric 相關依賴
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    
    // 共通模組
    implementation project(':common')
    include project(':common')
    
    // 包含 common 模組的所有依賴
    include 'org.yaml:snakeyaml:2.0'
    include 'com.mysql:mysql-connector-j:8.4.0'
    include 'com.zaxxer:HikariCP:5.0.1'
    include 'com.google.code.gson:gson:2.10.1'
    
    // 包含 SLF4J 實現 (避免日誌錯誤)
    include 'org.slf4j:slf4j-simple:2.0.7'
    
    // 測試依賴 - 統一版本管理
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.1'
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testImplementation "com.h2database:h2:${h2_version}"
}

// test 配置已在根 build.gradle 統一處理

processResources {
    def versionValue = project.version
    def minecraftVersionValue = project.minecraft_version
    def loaderVersionValue = project.loader_version
    
    inputs.property "version", versionValue
    inputs.property "minecraft_version", minecraftVersionValue
    inputs.property "loader_version", loaderVersionValue
    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": versionValue,
                "minecraft_version": minecraftVersionValue,
                "loader_version": loaderVersionValue
    }
}

jar {
    def archivesName = project.base.archivesName.get()
    from("../LICENSE") {
        rename { "${it}_${archivesName}"}
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}